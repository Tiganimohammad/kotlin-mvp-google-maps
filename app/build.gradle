apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def scooterApiUrl = ""
def googleMapsApiKey = ""

if (file("config.properties").canRead()) {
    print "Reading config.properties"
    Properties props = new Properties()
    props.load(new FileInputStream(file("config.properties")))

    scooterApiUrl = props["SCOOTER_API_URL"].toString()
    googleMapsApiKey = props["GOOGLE_MAPS_API_KEY"].toString()
} else {
    print "Unable to find config.properties."
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.brainasaservice.kotlin.kotlingooglemaps"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'SCOOTER_API_URI', '"'+scooterApiUrl+'"'
        resValue 'string', 'google_maps_key', googleMapsApiKey
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"

    implementation 'com.android.support:appcompat-v7:26.0.1'

    implementation 'com.google.android.gms:play-services-maps:11.2.0'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    provided 'javax.annotation:jsr250-api:1.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    implementation 'com.jakewharton.timber:timber:4.5.1'

    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'
}
